solarize () {
    local github_url='https://github.com'
    local github_dir=${HOME}/src/github.com

    local dircolors_repo=seebi/dircolors-solarized
    local dircolors_file=dircolors.ansi-dark

    if ! [[ -d ${github_dir%/}/${dircolors_repo} ]]; then
        git clone --filter=blob:none ${github_url}/${dircolors_repo} ${github_dir%/}/${dircolors_repo}
    fi

    local dircolors_filepath=${github_dir%/}/${dircolors_repo%/}/${dircolors_file}

    # set LS_COLORS
    if (( $+commands[dircolors] )); then
        eval $(dircolors -b ${dircolors_filepath}(N-.))
    elif (( $+commands[gdircolors] )); then
        eval $(gdircolors -b ${dircolors_filepath}(N-.))
    fi

    # set list-colors
    zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

    #set ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE
    ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=10"

    export SOLARIZED=1
}
# vim: ft=zsh
